\chapter{Results and discussion}
\label{chap4}

In this chapter, we discuss the possibilities of use of the extended \cmm compiled by the prototype compiler\cmmrepo described in \cref{chap3}.

\section{Functionality of the prototype compiler}
\label{sec:protoComp}

\subsection{Type inference of local declarations}

First, we demonstrate the simple properties of the type system. As expected, the compiler is able to infer types of local declarations. On a simple example of \li{gcd} procedure (\cref{lst:localDecl}) that contains variables declared with ``auto'' type, it is able to deduce the correct type for a local variable and the return value.

\begin{codex}
    \caption{Example of type-inferring local declarations}
    \label{lst:localDecl}
    \begin{center}
    %\begin{minipage}[t]{0.5\linewidth}
    \begin{minipage}{0.5\linewidth}
    \begin{lstlisting}
// input code
foreign "C" gcd(bits32 u,
                bits32 w)
                -> auto {
  auto r;
  while:
  if w != 0 {
    r = w;
    w = u % w;
    u = r;
    goto while;
  }

  return (u);
}
    \end{lstlisting}
    \end{minipage}%
    \begin{minipage}{0.5\linewidth}
    \begin{lstlisting}
// type-inferred code
foreign "C" gcd(bits32 u,
                bits32 w)
                -> bits32 {
  bits32 r;
  while:
  if w != 0 {
    r = w;
    w = u % w;
    u = r;
    goto while;
  }

  return (u);
}
    \end{lstlisting}
    \end{minipage}
    \end{center}
\end{codex}

\subsection{Generic programming using polymorphism}
\label{sec:reduction}

Type inference allows us to write polymorphic procedures, which are then correctly interpreted by the compiler and rewritten into an efficient code with correctly filled types for each call in a different context. The user does not have to define a procedure for each use separately. This is demonstrated by \cref{lst:reduction}. The code is ``monomorphized'' into code containing monomorphic definitions replacing the user-provided polymorphic ones with correctly inferred types.

Notably, compared to some other solutions (for example, Java and Python), which provide various ways to reduce code by polymorphism, the resulting code contains no runtime checks of types, and the code is as efficient as if it was written by hand for every specific type.

Monomorphization constitutes ``mangling'' the names of monomorphic copies of polymorphic procedures depending on their types. Mangling is performed so each generated definition has a unique name and can be correctly referenced from its corresponding call sites.

\begin{codex}
    \caption{Example use of generic programming (some names are truncated for brevity)}
    \label{lst:reduction}
    \begin{center}
    %\begin{minipage}[t]{0.5\linewidth}
    \begin{minipage}{0.5\linewidth}
    \begin{lstlisting}
// Shorter input code
// User-provided
// polymorphic definition
add(auto x, auto y) {
  return (x + y);
}

...

bits32 a1, b1;
auto c1;
a1 = 5;
b1 = 10;

c1 = add(b1, c1);

bits64 a2, b2;
auto c2;
a2 = 5;
b2 = 10;

c2 = add(b2, c2);
    \end{lstlisting}
    %\begin{minipage}[t]{0.5\linewidth}
    \end{minipage}%
    \begin{minipage}{0.5\linewidth}
    \begin{lstlisting}
// Automatically generated
// monomorphic definitions
_Madd_$f$Lb32...(bits32 x,
                 bits32 y) {
    return (x + y);
}
_Madd_$f$Lb64...(bits64 x,
                 bits64 y) {
    return (x + y);
}

...

bits32 a1, b1;
bits32 c1; // filled-in
a1 = 5;
b1 = 10;

c1 = _Madd_$f$Lb32...(a1, b1);

bits64 a2, b2;
bits64 c2; // filled-in
a2 = 5;
b2 = 10;

c2 = _Madd_$f$Lb64...(a2, b2);
    \end{lstlisting}
    \end{minipage}
    \end{center}
\end{codex}

\subsection{Simplified pointer dereferences}

In \cmm, a reference to memory (used for indirect writes and reads) has to specify the type of the memory location using the syntax \li{type[address]}. This syntax is close to C array indexing, but it consists of a type parameter and an address parameter (basically, a pointer). Introducing type inference into the language allowed us to make a simplification of this syntax with the type left out. The type is then inferred from the address parameter or the result. Any inferred type of the result or the address parameter then propagates to the other one. We show this in \cref{lst:fieldAccessors}.

\subsection{Translation into LLVM}

The prototype compiler translates the input code into the LLVM assembly language. This requires transforming the code into a simpler, flat representation with subsequent flow analysis, which then allows the code to be transcribed into the SSA representation of LLVM. Performing flow analysis allows for, among other things, identifying unreachable code, which is still type-inferred but not translated, and identifying uninitialized variables, which is followed by generating an error for each. The example in \cref{lst:translex} shows the translation without unreachable code and showcases a fairly unusual assignment statement, which is a feature of \cmm.

\subsection{Improvements over the algorithm W}

In our previous work \cite{klepl2020type}, we used a variation of algorithm W based on THIH \cite{jones1999typing}. We concluded that such an approach did not allow for multi-parameter typeclasses (\emph{MPTC}s) and general use of field accessors to \li{struct} fields; we required each field to have a unique name.

In the prototype compiler, we address this, and the prototype compiler's type inference interprets both MPTCs and field accessors without the requirement for unique names.

\begin{codex}
    \small
    \caption{Example of translation of an input source code into LLVM}
    \label{lst:translex}
    \begin{center}
    %\begin{minipage}[t]{0.5\linewidth}
    \begin{minipage}{0.5\linewidth}
    \begin{lstlisting}
// Source code
foreign "C" f(bits32 u,
              bits32 w)
              -> bits32 {
  auto r;

  if u == w {
    r, u = u, w;
    return (u);
  } else {
    w, r = u, w;
    u = r + w;
    return (u);
  }

  u = u * 2;
  w = u + r;

  return (w);
}
    \end{lstlisting}
    %\begin{minipage}[t]{0.5\linewidth}
    \end{minipage}%
    \begin{minipage}{0.5\linewidth}
    \begin{lstlisting}[style=llvmStyle]
; Output LLVM assembly
define external ccc  <{i32}>
@f(i32  %u_0, i32  %w_0)    {
LR$procedure0:
  %0 = icmp eq i32 %u_0, %w_0
  br i1 %0, label %F$then_1,
            label %F$else_1
F$then_1:
  %1 = phi i32
        [%u_0, %LR$procedure0]
  %2 = phi i32
        [%w_0, %LR$procedure0]
  %3 = insertvalue <{i32}>
        undef, i32 %2, 0
  ret <{i32}> %3
F$else_1:
  %4 = phi i32
        [%u_0, %LR$procedure0]
  %5 = phi i32
        [%w_0, %LR$procedure0]
  %6 = add   i32 %5, %4
  %7 = insertvalue <{i32}>
        undef, i32 %6, 0
  ret <{i32}> %7

  ; the last three statements
  ;  were unreachable
}
    \end{lstlisting}
    \end{minipage}
    \end{center}
\end{codex}

\subsubsection{Multi-parameter typeclasses}
\label{sec:mptcs}

Multi-parameter typeclasses allow us to overload procedures according to more than one type argument. This is useful for situations when the procedure is to be implemented differently depending on the parameter types. Perhaps, the procedure can be implemented more efficiently when the input types are the same. The example in \cref{lst:mptcs} shows exactly that. If both of the types are \li{bits32}, the instance adds the inputs together and returns the result. If the second argument is a pointer, it performs a more complicated operation on the first argument, represented by multiplication.

\begin{codex}
    \small
    \caption{Example of MPTC usage in the extended \cmm (some names are truncated for brevity)}
    \label{lst:mptcs}
    \begin{center}
    %\begin{minipage}[t]{0.5\linewidth}
    \begin{minipage}{0.5\linewidth}
    \begin{lstlisting}[basicstyle=\scriptsize\ttfamily]
// Source code
class C a b {
  m (auto (a) x, auto (b) y)
    -> auto (a);
}

instance C bits32 bits32 {
  m (auto u, auto w) {
    w = w + u;
    return (w);
  }
}

instance C bits32 ptr(auto) {
  m (auto u, auto w) {
    u = u * 4;
    return (u);
  }
}

foreign "C"
f(bits32 x, ptr(bits64) y) {
  x = m(x, y);

  return (x);
}

foreign "C"
g(bits32 x) {
  x = m(x, x);

  return (x);
}
    \end{lstlisting}
    %\begin{minipage}[t]{0.5\linewidth}
    \end{minipage}%
    \begin{minipage}{0.5\linewidth}
    \begin{lstlisting}[style=llvmStyle, basicstyle=\scriptsize\ttfamily]
; Output LLVM assembly
define external ccc  <{i32}>
  @_Mm_$f$Lb32b64...(
    i32  %u_0, i64*  %w_0) {
LR$procedure2:
  %0 = mul   i32 %u_0, 4
  %1 = insertvalue <{i32}>
        undef, i32 %0, 0
  ret <{i32}> %1
}

define external ccc  <{i32}>
  @_Mm_$f$Lb32b32...(
    i32  %u_0, i32  %w_0) {
LR$procedure1:
  %0 = add   i32 %w_0, %u_0
  %1 = insertvalue <{i32}>
        undef, i32 %0, 0
  ret <{i32}> %1
}

define external ccc  <{i32}>
  @f(i32  %x_0, i64*  %y_0) {
LR$procedure3:
  %0 =  call ccc  <{i32}>
          @_Mm_$f$Lb32b64...(
            i32  %x_0,
            i64*  %y_0)
  %1 = extractvalue <{i32}>%0,0
  %2 = insertvalue <{i32}>
        undef, i32 %1, 0
  ret <{i32}> %2
}

define external ccc  <{i32}>
  @g(i32  %x_0) {
LR$procedure4:
  %0 =  call ccc  <{i32}>
          @_Mm_$f$Lb32b32...(
            i32  %x_0,
            i32  %x_0)
  %1 = extractvalue <{i32}>%0,0
  %2 = insertvalue <{i32}>
        undef, i32 %1, 0
  ret <{i32}> %2
}
    \end{lstlisting}
    \end{minipage}
    \end{center}
\end{codex}

\subsubsection{User-specified functional dependencies}
\label{sec:uspecFD}

Sometimes, a procedure can have returns of different types depending on the given arguments. In many instances, these types are closely related to argument types, and they can be derived from type algebra. For such cases, regular (multi-parameter) typeclasses are enough, as demonstrated by other examples. But, sometimes, the types of the returned values are not algebraically dependent on the argument types. The often mentioned field accessors are an example of this.

The prototype compiler allows the definition of functional dependencies for arbitrary user-defined typeclass. Simple use of this is demonstrated in \cref{lst:fundeps}. The compiler correctly type-infers the result types according to the given functional dependency.

\begin{codex}
    \caption{Example use of functional dependencies (some names are truncated for brevity)}
    \label{lst:fundeps}
    \begin{center}
    %\begin{minipage}[t]{0.5\linewidth}
    \begin{minipage}{0.5\linewidth}
    \begin{lstlisting}[basicstyle=\scriptsize\ttfamily]
// Source code

// a type class with
//  a fun. dependency: a -> b
class C a b | a -> b {
  c (auto (a) x) -> auto (b);
}

instance C bits32 bits32 {
  c (auto u) {
    return (u);
  }
}

instance C bits64 bits32 {
  c (auto u) {
    return (0);
  }
}

bits32 x;
auto c_x;
bits64 y;
auto c_y;

...

c_x = c(x);
c_y = c(y);
    \end{lstlisting}
    %\begin{minipage}[t]{0.5\linewidth}
    \end{minipage}%
    \begin{minipage}{0.5\linewidth}
    \begin{lstlisting}[basicstyle=\scriptsize\ttfamily]
// Monomorphized code
//  with interpreted
//  functional dependencies
_Mc_$f$Lb32...(bits32 u) {
  return (u);
}
_Mc_$f$Lb64...(bits64 u) {
  return (0);
}

bits32 x;
// type-inferred by return:
bits32 id_x;

bits64 y;
// type-inferred by return:
bits32 c_y;

...

c_x = _Mc_$f$Lb32...(x);
c_y = _Mc_$f$Lb64...(y);
    \end{lstlisting}
    \end{minipage}
    \end{center}
\end{codex}

\subsubsection{Field accessors}

Field accessors allow us to introduce further semantics into structs. This is useful in situations where each object in a certain model has to contain some information that characterizes the given object. It can be the object's identifier or age, and perhaps, it can have different types for different objects. For example, one part of the model can use 32-bit wide identifiers and another 64-bit wide. This is where the approach from the previous work did not suffice.

With the prototype implementation based on deferred solving with MPTCs and functional dependencies, type-inferring general field accessors is possible, as shown in \cref{lst:fieldAccessors}. In the example, the compiler type-infers all relevant types; only \li{struct} definitions are typed explicitly.

\begin{codex}
    \caption{Example use of field accessors (some names are truncated for brevity)}
    \label{lst:fieldAccessors}
    \begin{center}
    %\begin{minipage}[t]{0.5\linewidth}
    \begin{minipage}{0.45\linewidth}
    \begin{lstlisting}[basicstyle=\scriptsize\ttfamily]
// Source code
struct A {
  id: bits32;
}

struct B {
  id: bits64;
}

update_id(auto s,
          auto x) {
  // indirect write
  // to 'id' field
  [s->id] = x;
  return ();
}

access_id(auto s) {
  auto x;
  // indirect read
  x = [s->id];
  return (x);
}

...

stackdata {
  a: A;
  b: B;
}

auto x;
auto y;

= update_id(a, 5);
= update_id(b, 10);

x = access_id(a);
y = access_id(b);
    \end{lstlisting}
    %\begin{minipage}[t]{0.5\linewidth}
    \end{minipage}%
    \begin{minipage}{0.55\linewidth}
    \begin{lstlisting}[style=llvmStyle,basicstyle=\scriptsize\ttfamily]
; Output LLVM assembly
define external ccc  <{}>
  @_Mupdate...A({i32}*  %s_0,
                i32  %x_0) {
LR$procedure0:
  %0 = getelementptr {i32}, {i32}* %s_0,
                      i32 0, i32 0
  store   i32 %x_0, i32* %0
  ret <{}> <{  }>
}
; define @_Mupdate...B <- did not fit

define external ccc  <{i32}>
  @_Maccess...A({i32}*  %s_0) {
LR$procedure1:
  %0 = getelementptr {i32}, {i32}* %s_0,
                      i32 0, i32 0
  %1 = load   i32, i32* %0
  %2 = insertvalue <{i32}>
        undef, i32 %1, 0
  ret <{i32}> %2
}
; define @_Maccess...B <- did not fit

...

%0 = alloca {i32}
%1 = alloca {i64}
%2 = call ccc  <{}>
       @_Mupdate...A({i32}* %0, i32 5)
%3 = call ccc  <{}>
       @_Mupdate...B({i64}* %1, i64 10)
%4 = call ccc  <{i32}>
       @_Maccess...A({i32}* %0)
%5 = extractvalue <{i32}> %4, 0
%6 = call ccc  <{i64}>
       @_Maccess...B({i64}* %1)
%7 = extractvalue <{i64}> %6, 0

    \end{lstlisting}
    \end{minipage}
    \end{center}
\end{codex}

\subsection{Typeclass-based resource management}
\label{sec:tcResMan}

Let us say we want to perform some resource management action on every procedure exit. Perhaps, we opened a file, and we want to close it. In practice, this is quite common, and it led to, for example, developing RAII in C++ or try/finally pattern in C\#. For this purpose, in the prototype compiler, we can use \li{new}-specialized stack-allocated objects.

If an object carries the \li{new} specialization, the compiler calls the \li{drop} procedure on the pointer reference to the object before every exit from the procedure. This \li{drop} call then carries out the resource management action as defined for the object's type. This is demonstrated by \cref{lst:resource}.

\begin{codex}
    \small
    \caption{Example use of automatic resource management (some names are truncated for brevity)}
    \label{lst:resource}
    \begin{center}
    %\begin{minipage}[t]{0.5\linewidth}
    \begin{minipage}{0.4\linewidth}
    \begin{lstlisting}[basicstyle=\scriptsize\ttfamily]
// Source code
struct X {
  x: bits32;
}

struct Y {
  x: bits64;
}

class Managed a {
drop(ptr(auto(a)) u)
  -> ;
}

instance Managed X {
  drop(auto u) {
    auto y;
    y = [u->x];
    return ();
  }
}

instance Managed Y {
  drop(auto w) {
    return ();
  }
}

foreign "C"
f(bits32 a) {
  stackdata {
    F: new X;
    G: new Y;
  }

  return ();
}
    \end{lstlisting}
    %\begin{minipage}[t]{0.5\linewidth}
    \end{minipage}%
    \begin{minipage}{0.6\linewidth}
    \begin{lstlisting}[style=llvmStyle,basicstyle=\scriptsize\ttfamily]
; Output LLVM assembly
define external ccc <{}>
@_Mdrop...X({i32}*  %u_0) {
LR$procedure1:
  %0 = getelementptr {i32}, {i32}* %u_0,
                      i32 0, i32 0
  %1 = load   i32, i32* %0
  ret <{}> <{  }>
}


define external ccc <{}>
@_Mdrop...Y({i64}*  %w_0) {
LR$procedure2:
  ret <{}> <{  }>
}


define external ccc <{}>
@f(i32  %a_0) {
LR$procedure3:
  %0 = alloca {i32}
  %1 = alloca {i64}

  ; automatically generated
  ; resource management:

  ; represented by X
  %2 =  call ccc  <{}>
          @_Mdrop...X({i32}*  %0)
  ; represented by Y
  %3 =  call ccc  <{}>
          @_Mdrop...Y({i64}*  %1)
  ret <{}> <{  }>
}
    \end{lstlisting}
    \end{minipage}
    \end{center}
\end{codex}

\subsubsection{Type-inferred resource management}

It may happen that the resource management action depends on a type parameter. Perhaps, this can be due to the architectural specifics of the target platform or due to specifics of a certain component. The system is ready for such a situation -- we can combine automatic resource management and type inference of the resource objects (with or without typeclass overloading) as demonstrated by \cref{lst:polyResource}

\begin{codex}
    \small
    \caption{Example use of type-inferred resource management (some names are truncated for brevity)}
    \label{lst:polyResource}
    \begin{center}
    %\begin{minipage}[t]{0.5\linewidth}
    \begin{minipage}{0.4\linewidth}
    \begin{lstlisting}[basicstyle=\scriptsize\ttfamily]
// Source code
struct X {
  x: bits32;
}

struct Y {
  x: bits64;
}

drop(auto u) {
  // ...
  return ();
}

foreign "C"
f(ptr(X) s,
  ptr(Y) t) {
  stackdata {
    // type-inferred
    //   resource
    //   objects
    x: new auto;
    y: new auto;
  }

  [x] = [s->x];
  [y] = [t->x];

  return ();
}
    \end{lstlisting}
    %\begin{minipage}[t]{0.5\linewidth}
    \end{minipage}%
    \begin{minipage}{0.6\linewidth}
    \begin{lstlisting}[style=llvmStyle,basicstyle=\scriptsize\ttfamily]
; Output LLVM assembly
define external ccc <{}>
@_Mdrop...(i32*  %u_0) {
LR$procedure0:
  ; ...
  ret <{}> <{  }>
}


define external ccc <{}>
@_Mdrop...(i64*  %u_0) {
LR$procedure0:
  ; ...
  ret <{}> <{  }>
}


define external ccc <{}>
@f({i32}*  %s_0, {i64}*  %t_0) {
LR$procedure1:
  %0 = alloca i32
  %1 = alloca i64
  %2 = getelementptr {i32}, {i32}* %s_0,
                      i32 0, i32 0
  %3 = load   i32, i32* %2
  store   i32 %3, i32* %0
  %4 = getelementptr {i64}, {i64}* %t_0,
                      i32 0, i32 0
  %5 = load   i64, i64* %4
  store   i64 %5, i64* %1
  %6 =  call ccc  <{}>
          @_Mdrop...(i32*  %0)
  %7 =  call ccc  <{}>
          @_Mdrop...(i64*  %1)
  ret <{}> <{  }>
}
    \end{lstlisting}
    \end{minipage}
    \end{center}
\end{codex}

\subsubsection{Automatic resource management with \texttt{dropped} statements}

Sometimes, when we use automatic resource management, there exists a special case in which we do not want to perform the resource management action. This may apply to cases where the resource is a file handle of a file we failed to open. In the prototype compiler, we can use \li{dropped} statement with a named reference to a resource object to specify that the corresponding resource management action does not happen automatically (but may still happen manually) in the given part of the scope. We demonstrate its use in \cref{lst:manual}.


\begin{codex}
    \small
    \caption{Example use of automatic resource management with \li{dropped} statements (some names are truncated for brevity)}
    \label{lst:manual}
    \begin{center}
    %\begin{minipage}[t]{0.5\linewidth}
    \begin{minipage}{0.4\linewidth}
    \begin{lstlisting}[basicstyle=\scriptsize\ttfamily]
// Source code
drop(auto a) {
  return ();
}

foreign "C"
f(bits32 a) {
  stackdata {
    x:y: new bits32;
  }

  if a == 0 {
    dropped x;
    // manual drop
    // (then_1)
    = drop(x);
    return ();
  }

  if a == 5 {
    if a == 10 {
  // automatic drop
  // (then_3)
      return ();
    } else {
      dropped y;
      // no drop
      return ();
    }
  }

  // automatic drop
  // (fi_2)
  return ();
}
    \end{lstlisting}
    %\begin{minipage}[t]{0.5\linewidth}
    \end{minipage}%
    \begin{minipage}{0.6\linewidth}
    \begin{lstlisting}[style=llvmStyle,basicstyle=\scriptsize\ttfamily]
; Output LLVM assembly
define external ccc <{}>
@_Mdrop...(i32*  %a_0) {
LR$procedure0:
  ret <{}> <{  }>
}


define external ccc <{}>
@f(i32  %a_0) {
LR$procedure1:
  %0 = alloca i32
  %1 = icmp eq i32 %a_0, 0
  br i1 %1, label %F$then_1,
            label %F$fi_1
F$then_1:
  %2 =  call ccc  <{}>
          @_Mdrop...(i32*  %0)
  ret <{}> <{  }>
F$fi_1:
  %3 = phi i32 [%a_0, %LR$procedure1]
  %4 = icmp eq i32 %3, 5
  br i1 %4, label %F$then_2,
            label %F$fi_2
F$then_2:
  %5 = phi i32 [%3, %F$fi_1]
  %6 = icmp eq i32 %5, 10
  br i1 %6, label %F$then_3,
            label %F$else_3
F$fi_2:
  %7 =  call ccc  <{}>
          @_Mdrop...(i32*  %0)
  ret <{}> <{  }>
F$then_3:
  %8 =  call ccc  <{}>
          @_Mdrop...(i32*  %0)
  ret <{}> <{  }>
F$else_3:
  ret <{}> <{  }>
}
    \end{lstlisting}
    \end{minipage}
    \end{center}
\end{codex}

\section{Possible applications to other languages}

\subsection{Subtyping in systems programming}

The subset of subtyping modeled by our proposed type system can be used to check and infer various secondary properties of objects. Throughout the thesis, we demonstrated its use on constnesses and data kinds of the \cmm language. Connecting the analysis of these secondary properties to the type system allows for tying the constraints on these properties only to a particular typing (as defined in \cref{sec:typesystem}), for example, by defining each typeclass instance with a different set of constraints.

\subsection{C subtyping}

In the previous work \cite{klepl2020type}, we identified a weakness of the then-used type system with regards to the \li{const} qualifier from the C language \cite{cstandard2018}. Let us examine whether its semantics fit the type system we introduced in \cref{sec:typesystem}.

The semantics of the C \li{const} qualifier (as specified by \cite{cstandard2018}), quite differ from the constness introduced in \cref{sec:repre}, despite the similarity of names. For our purposes, we simplify the semantics into two cases:

\begin{enumerate}
    \item If the \li{const} qualifier is applied to a variable (for example, \li{char *const p}), the type of the variable is the same as if it was not \li{const}-qualified. Any attempt to assign to this variable is undefined behavior. \label{constvar}

    \item If the \li{const} qualifier is applied to a type pointed to by a certain pointer (for example, \li{char const *p}), any assignments to the pointer from pointers pointing to a non-\li{const}-qualified version of the same type are valid, and the two pointers have the same value afterward. The reverse assignment would be, again, an undefined behavior. \label{pointerSubtype}

    In our formalism, this means that \li{T *} is less general than \li{T const *}.
\end{enumerate}

We can model the semantics of the case number \ref{pointerSubtype} (case number \ref{constvar} is identified syntactically) via introducing two new subtype dimensions, one of which is only relevant to pointer types (in simplification; we could then generalize this to array types and other aggregate types), let us call this dimension the \emph{C-const dimension}. And one other dimension to represent all subtype relations, let us call this dimension the \emph{assigment dimension}.

\begin{defn}[C-const dimension]
    The C-const subtype dimension $C$ is defined by two constants $\mtt{NonConst}, \mtt{Const}$ and an ordering $\top = \mtt{Const} >_C \mtt{NonConst} = \bot$.

    This dimension is only relevant to pointer types, other types being either unconstrained or constrained to specifically one of the constants (which is then sometimes called canonical -- see \cite{tiuryn1999subtyping}).
\end{defn}

\begin{defn}[Assignment dimension]
    The assignment subtype dimension $A$ is defined by one constant $\bot_A$.
\end{defn}

In the following, we use $a \sim_\chi b$ as a shortcut for $a \leq_\chi b \land b \leq_\chi a$, where $\chi$ is a dimension of the type system.

\begin{itemize}
    \item Each type definition (or primitive type: for example, \li{int}) $t$ is constrained: $t \sim_C \mtt{NonConst}$ (on top of any other constraints).

    \item Any declaration \li{T const *} constrains the type $t$ of the declared variable: $(\forall \mtt{dimension}\ s, s \neq C. t \sim_s T\ast), t \sim_C \mtt{Const}$ (this models that applying \li{const} is idempotent).

    \item For (generalized) assignments with operand types $t_l, t_r$, we put $t_l \geq_s t_r$ for each dimension $s$ and $t_l \sim_T t_r$. (we say ``generalized assignments'' as we consider even actual arguments to procedures and return operands, etc.).
\end{itemize}

To sufficiently constrain the space, so the modeled semantics are the same as the semantics of the C \li{const} qualifier, after deciding all typings and before deciding any subtype dimensions, we introduce new constraints.

These constraints are derived from the assignment dimension $A$ for every two types such that $t \leq_A t'$. For example, if $t \equiv T\ast\ast$ and $t' \equiv T'\ast\ast$, we put $T \sim T'$ (as this is still part of the semantic case number \ref{pointerSubtype}). We will not go into further details as this is outside the scope of this work. After this step, the type inference continues as usual.

We leave this for future work together with considering the possibility of generating all subtype constraints after deciding the typings for the given subprogram component.

\subsection{Automatic resource management in practical systems programming}
\label{sec:autoC}

We show how to use the results of this thesis for simplifying resource management in system programming languages.

\citet{kernelStyle} proposed the following convention in \citetitle{kernelStyle}:

\begin{quote}
  The goto statement comes in handy when a procedure exits from multiple locations and some common work such as cleanup has to be done. If there is no cleanup needed then just return directly.
\end{quote}

In \cref{cex:cleanup}, we show that the automatic resource management features introduced by the extended \cmm can serve as an alternative to this convention. In particular, the cleanup section is replaced by a definition of a resource-representing object and accompanying \li{drop} procedure.

\paragraph{``If there is no cleanup needed, then just return directly''} In order to fully serve as a practical alternative to the \li{goto cleanup} pattern in a general case, we also introduce \li{dropped} statement. \li{dropped} statement takes a named alias to the resource-representing object, and the resource management action is not performed for this object on exits, which follows in the control flow of the given procedure.

One improvement over the \li{goto cleanup} pattern is that this can easily scale with more actions tied to multiple resource-representing objects, each such action then possibly being prevented by a separate \li{dropped} statement on some subset of control flow paths.

\begin{codex}
  \caption{Automatic resource management as an alternative to the \texttt{goto cleanup} design-pattern (the C code taken from \citetitle{kernelStyle} \cite{kernelStyle}; \texttt{drop} procedure is written just once for multiple buffers)}
  \label{cex:cleanup}
  \begin{minipage}{0.5\linewidth}
    \begin{lstlisting}[basicstyle=\scriptsize\ttfamily]
// C
int fun(int a)
{
  // explicit types,
  //  'result' used just
  //  to store the return
  int result = 0;
  char *buffer;

  buffer = kmalloc(SIZE,
                   GFP_KERNEL);
  if (!buffer)
    return -ENOMEM;

  if (condition1) {
    while (loop1) {
      ...
    }
    // if there was just return
    //  (not obviously incorrect use)
    //  the buffer would leak
    result = 1;
    goto out_free_buffer;
  }
  ...
out_free_buffer:
  kfree(buffer);
  return result;
}
    \end{lstlisting}
  \end{minipage}%
  \begin{minipage}{0.5\linewidth}
    \begin{lstlisting}[basicstyle=\scriptsize\ttfamily]
// Extended C--
drop(ptr (ptr (bits8)) buffer) -> {
  kfree([buffer]);
  return ();
}

foreign "C"
fun(bits32 a) -> bits32 {
  stackdata {
    buffer: new auto;
  }

  [buffer] = kmalloc(SIZE,
                     GFP_KERNEL);
  if [buffer] != 0 {
    dropped buffer;
    // no call to 'drop(buffer)'
    return -(ENOMEM);
  }

  if condition1 {
    while:
    if loop1 {
      ...
      goto while;
    }
    return (1);
  }
  ...
  return (0);
}
    \end{lstlisting}
  \end{minipage}%
\end{codex}

\section{Relation to other resource management systems}
\label{sec:OtherRAII}

Multiple constructs in other languages implement the automatic resource management action that has to happen on every scope exit. We will look at two: C++ RAII and C\# \li{try\{...\} finally\{...\}}.

C++ calls the resource management action \emph{destructor}, and, in the given scope, it is called on every exit. In C\#, \li{finally \{ ... \}} serves for this purpose as well. In the extended \cmm, the resource management action is specified by defining a resource-representing object with the \li{new} keyword. This object then constitutes a call of the \li{drop} method on every scope exit.

If we specify an automatic resource management action for some scope in C++ or C\#, it is often difficult to specify a set of scope exits that do not issue the action. For example, in C++, the action is always executed, and the non-action is available only as a side effect of optimization or the moving convention, which may be marked by \li{std::move}. The extended \cmm offers a relatively direct approach to the problem: all exits execute the dropping action by default for safety unless the exit follows a \li{dropped} statement, at which point no action is executed at all.

Additionally, the initialization of a resource in the extended \cmm is not directly linked to its release (\li{drop}), as is the case with C++, where the constructors and destructors are symmetrical. To implement this kind of symmetric behavior, we could enforce executing resource initialization code at the procedure entry for each resource-representing object using another method from the same typeclass.
